# Load .env files by default
set dotenv-load := true


# an alias to allow us to use this recipe from the justfile in the parent directory
_dotenv:
    {{ just_executable() }} --justfile {{ justfile_directory() }}/../justfile _dotenv


# manually run the docker db. Useful if you want to use it but with venv based dev env
db: 
    docker compose up -d db

# stop and remove all containers
clean:
    #!/bin/bash
    set -eux
    docker compose down
    docker container prune --force --filter label=com.docker.compose.project=$COMPOSE_PROJECT_NAME

# remove all state
clean-volumes: clean
    #!/bin/bash
    set -eux
    docker volume rm -f job-server_postgres_data


# restore the db from a dump file. Will wipe and reset your current docker dev db.
restore-db dump="jobserver.dump": clean-volumes  db
    #!/bin/bash
    set -eux
    path=$(realpath ../{{ dump }})
    docker compose run -v "$path:/tmp/jobserver.dump" --entrypoint bash --rm dev -c "pg_restore --clean --if-exists --no-acl --no-owner -d \$DATABASE_URL /tmp/jobserver.dump"
